plugins {
    id "fabric-loom" version '0.11-SNAPSHOT'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version '1.6.10'
}

archivesBaseName = "Droids"
group = "com.github.rochedo098"

repositories {
    maven {
        name = "Siphalor's Maven"
        url = "https://maven.siphalor.de"
    }

    maven {
        name = "Modmuss50"
        url = "https://maven.modmuss50.me/"
        content {
            includeGroup "teamreborn"
        }
    }

    maven {
        name = "Saps"
        url = "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.latvian.mods"
        }
    }

    maven {
        name "JitPack"
        url "https://jitpack.io"
    }
}

dependencies {
    // Minecraft and Fabric Loader
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

    include modImplementation("de.siphalor:nbtcrafting-1.18:${project.nbtcrafting_version}")
    include modImplementation("teamreborn:energy:${project.techreborn_energy}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    include modApi("dev.latvian.mods:rhino-fabric:${rootProject.rhino_version}")
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from ("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

compileKotlin.kotlinOptions.jvmTarget = "17"
