plugins {
    id "fabric-loom" version '0.11-SNAPSHOT'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
}

archivesBaseName = "Droids"
version = "0.1.0"
group = "com.github.rochedo098"

dependencies {
    // Fabric API.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

    modImplementation include("de.siphalor:nbtcrafting-1.18:${project.nbtcrafting_version}")
    modImplementation include("teamreborn:energy:${project.techreborn_energy}")
    //modImplementation include("dev.lazurite:lattice:${project.lattice_version}") //LibGUI

    api project(path: ":DroidsAPI", configuration: "namedElements")
    include project(":DroidsAPI")
}

compileKotlin.kotlinOptions.jvmTarget = "16"

allprojects {
    apply plugin: "fabric-loom"
    apply plugin: "maven-publish"

    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16

    repositories {
        maven {
            name = "Lazurite"
            url = "https://lazurite.dev/releases"
        }

        maven {
            name = "ModMenu"
            url = "https://maven.terraformersmc.com/releases/"
        }

        maven {
            name = "Siphalor's Maven"
            url = "https://maven.siphalor.de"
        }

        maven {
            name = "Modmuss50"
            url = "https://maven.modmuss50.me/"
            content {
                includeGroup "teamreborn"
            }
        }

        maven {
            name = "Saps"
            url = "https://maven.saps.dev/minecraft"
            content {
                includeGroup "dev.latvian.mods"
            }
        }

        maven {
            name = "SheDaniel"
            url = "https://maven.shedaniel.me"
        }

        maven {
            name = "Jitpack"
            url = "https://jitpack.io"
        }
    }

    dependencies {
        // Minecraft and Fabric Loader
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    }

    processResources {
        inputs.property "version", project.version
        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    tasks.withType(JavaCompile) {
        it.options.encoding = "UTF-8"
        it.options.release = 16
    }

    java {
        withSourcesJar()
    }

    /*
    publishing {
        publications {
            from(components["java"])
        }

        repositories {
            mavenLocal()
        }
    }
    */

    jar {
        from ("LICENSE") {
            rename { "${it}_${project.archivesBaseName}"}
        }
    }
}
